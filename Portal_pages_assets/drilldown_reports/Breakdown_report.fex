-* File: IBFS:/localhost/WFC/Repository/Aquilini_Brands/Report1.fex Created by WebFOCUS AppStudio
-*-SET &ECHO=ON;
-DEFAULT &RECIPIENT_STATE_NAME = _FOC_NULL;
-DEFAULT &ORDER_MONTH_FROM = _FOC_NULL;
-DEFAULT &ORDER_MONTH_TO = _FOC_NULL;
-DEFAULT &PRODUCT_CATEGORY = _FOC_NULL;
-DEFAULT &PROD_NAME = _FOC_NULL;
-DEFAULT &INCLUDE_WA = 'Y';
-DEFAULT &DRILL_FIELD= 'FOC_NONE';
-DEFAULT &DRILL_VALUE= 'FOC_NONE';
-DEFAULTH &DRILL_FIELD2= 'FOC_NONE';
-DEFAULTH &DRILL_VALUE2= 'FOC_NONE';

-DEFAULT &BREAKDOWN_MEASURE = 'WF_RETAIL.WF_RETAIL_SALES.REVENUE_US';
-DEFAULT &BREAKDOWN_DIMENSION = 'PRODUCT_NAME';
-SET &DRILL_FIELD_HEADING=TOKEN(&DRILL_FIELD,'.',-1);
-SET &DRILL_FIELD2_HEADING=TOKEN(&DRILL_FIELD2,'.',-1);

-SET &DRILL_VALUE=IF &DRILL_VALUE CONTAINS '''' THEN TOKEN(&DRILL_VALUE,'''',2) ELSE &DRILL_VALUE;
-SET &DRILL_VALUE2=IF &DRILL_VALUE2 CONTAINS '''' THEN TOKEN(&DRILL_VALUE2,'''',2) ELSE &DRILL_VALUE2;

-SET &BREAKDOWN_DIMENSION_HEADING=TOKEN(&BREAKDOWN_DIMENSION,'.',-1);
-TYPE INCLUDE_WA &INCLUDE_WA.(<Yes,Y>,<No,N>).Include Web Sales.
-SET &WHERE_EXCLUDE_STATE=IF &INCLUDE_WA EQ 'N' THEN 'Web' ELSE '_FOC_NULL';
-SET &HEADING_EXCLUDE_STATE=IF &INCLUDE_WA EQ 'N' THEN '(excl. Web Sales)' ELSE ' ';

SET ARVERSION=2
TABLE FILE WF_RETAIL
SUM
     &BREAKDOWN_MEASURE.(<Revenue,WF_RETAIL.WF_RETAIL_SALES.REVENUE_US>,<Quantity Sold,WF_RETAIL.WF_RETAIL_SALES.QUANTITY_SOLD>,<Profit,WF_RETAIL.WF_RETAIL_SALES.GROSS_PROFIT_US>).Grouping.
BY &BREAKDOWN_DIMENSION.(<Product,WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_NAME>,<Product Category,WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_CATEGORY>,<Brand,WF_RETAIL.WF_RETAIL_PRODUCT.BRAND>,<Store Name,WF_RETAIL.WF_RETAIL_STORE_SALES.STORE_NAME>,<Sales Quarter,WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_QTR_COMPONENT>,<Sales Month,WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_MONTH_COMPONENT>,<Sales Date,WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_MONTH_COMPONENT>,<Recipient State,WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.STATE_PROV_NAME>,<Recipient City,WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.CITY_NAME>,<Sales Id,WF_RETAIL.WF_RETAIL_SALES.ID_SALES>).Measure.
WHERE WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_CATEGORY EQ &PRODUCT_CATEGORY.(OR(FIND WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_CATEGORY IN RETAIL_SAMPLES/WF_RETAIL |FORMAT=A64V,SORT=ASCENDING)).Product Category:.;
WHERE WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_NAME EQ &PROD_NAME.(OR(FIND WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_NAME IN RETAIL_SAMPLES/WF_RETAIL |FORMAT=A128V,SORT=ASCENDING ,WITHIN=PRODUCT_CATEGORY)).Product Name:.;
WHERE WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.STATE_PROV_NAME EQ &RECIPIENT_STATE_NAME.(OR(FIND WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.STATE_PROV_NAME IN RETAIL_SAMPLES/WF_RETAIL |FORMAT=A40V,SORT=ASCENDING)).State or Province:.;
WHERE WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_MONTH_COMPONENT GE &ORDER_MONTH_FROM.(FIND WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_MONTH_COMPONENT IN RETAIL_SAMPLES/WF_RETAIL |FORMAT=YYMDm,SORT=ASCENDING).Order Month From:.QUOTEDSTRING;
WHERE WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_MONTH_COMPONENT LE &ORDER_MONTH_TO.(FIND WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_MONTH_COMPONENT IN RETAIL_SAMPLES/WF_RETAIL |FORMAT=YYMDm,SORT=DESCENDING).Order Month To:.QUOTEDSTRING;
WHERE WF_RETAIL.WF_RETAIL_STORE_SALES.STORE_TYPE NE '&WHERE_EXCLUDE_STATE';
WHERE WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.COUNTRY_NAME EQ 'Canada' OR 'United States';

WHERE &DRILL_FIELD EQ '&DRILL_VALUE';
WHERE &DRILL_FIELD2 EQ '&DRILL_VALUE2';
HEADING
"Detail Breakdown by &BREAKDOWN_DIMENSION_HEADING"
"&HEADING_EXCLUDE_STATE"
"for &DRILL_FIELD_HEADING = &DRILL_VALUE "
"and &DRILL_FIELD2_HEADING = &DRILL_VALUE2 "

ON TABLE SET PAGE-NUM NOLEAD
ON TABLE SET ASNAMES ON
ON TABLE SET AUTOFIT ON
ON TABLE SUMMARIZE AS 'Total'
ON TABLE PCHOLD FORMAT AHTML
ON TABLE SET HTMLEMBEDIMG ON
ON TABLE SET HTMLCSS ON
ON TABLE SET STYLE *
     INCLUDE = IBFS:/EDA/EDASERVE/_EDAHOME/ETC/warm.sty,
$
TYPE=REPORT,
$
     GRAPHTYPE=DATA,
     COLUMN=N2,
     GRAPHCOLOR='SILVER',
$
ENDSTYLE
END
