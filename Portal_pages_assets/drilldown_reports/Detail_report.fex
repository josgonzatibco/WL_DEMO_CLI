-DEFAULT &RECIPIENT_STATE_NAME = _FOC_NULL;
-DEFAULT &ORDER_MONTH_FROM = _FOC_NULL;
-DEFAULT &ORDER_MONTH_TO = _FOC_NULL;
-DEFAULT &PRODUCT_CATEGORY = _FOC_NULL;
-DEFAULT &PROD_NAME = _FOC_NULL;
-DEFAULT &DRILL_FIELD= 'FOC_NONE';
-DEFAULT &DRILL_VALUE= 'FOC_NONE';
-DEFAULTH &DRILL_FIELD2= 'FOC_NONE';
-DEFAULTH &DRILL_VALUE2= 'FOC_NONE';
-DEFAULT &INCLUDE_WA = 'Y';
-TYPE INCLUDE_WA &INCLUDE_WA.(<Yes,Y>,<No,N>).Include Web Sales.
-SET &WHERE_EXCLUDE_STATE=IF &INCLUDE_WA EQ 'N' THEN 'Web' ELSE '_FOC_NULL';
-SET &HEADING_EXCLUDE_STATE=IF &INCLUDE_WA EQ 'N' THEN '(excl. Web Sales)' ELSE ' ';
-SET &DRILL_FIELD_HEADING=TOKEN(&DRILL_FIELD,'.',-1);
-SET &DRILL_FIELD2_HEADING=TOKEN(&DRILL_FIELD2,'.',-1);
-SET &DRILL_VALUE=IF &DRILL_VALUE CONTAINS '''' THEN TOKEN(&DRILL_VALUE,'''',2) ELSE &DRILL_VALUE;
-SET &DRILL_VALUE2=IF &DRILL_VALUE2 CONTAINS '''' THEN TOKEN(&DRILL_VALUE2,'''',2) ELSE &DRILL_VALUE2;

SET ARVERSION=2
TABLE FILE WF_RETAIL
SUM WF_RETAIL.WF_RETAIL_SALES.QUANTITY_SOLD AS 'Qty,Sold'
WF_RETAIL.WF_RETAIL_SALES.REVENUE_US
WF_RETAIL.WF_RETAIL_SALES.GROSS_PROFIT_US AS 'Gross,Profit'

BY WF_RETAIL.WF_RETAIL_SALES.ID_SALES AS 'Id'
BY WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_CATEGORY
BY WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_SUBCATEG
BY WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_NAME
BY WF_RETAIL.WF_RETAIL_PRODUCT.BRAND
BY WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_QTR_COMPONENT AS 'Sale Yr,Qtr'
BY WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_MONTH_COMPONENT AS 'Sale Yr,Mth'
BY WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_DAY_COMPONENT
BY WF_RETAIL.WF_RETAIL_CUSTOMER.FULLNAME
-*BY WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.ADDRESS_LINE_1
BY WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.CITY_NAME
BY WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.STATE_PROV_NAME
BY WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.COUNTRY_NAME
BY WF_RETAIL.WF_RETAIL_STORE_SALES.STORE_NAME
-*BY WF_RETAIL.WF_RETAIL_STORE_SALES.STORE_TYPE

WHERE WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_CATEGORY EQ &PRODUCT_CATEGORY.(OR(FIND WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_CATEGORY IN RETAIL_SAMPLES/WF_RETAIL |FORMAT=A64V,SORT=ASCENDING)).Product Category:.;
WHERE WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_NAME EQ &PROD_NAME.(OR(FIND WF_RETAIL.WF_RETAIL_PRODUCT.PRODUCT_NAME IN RETAIL_SAMPLES/WF_RETAIL |FORMAT=A128V,SORT=ASCENDING ,WITHIN=PRODUCT_CATEGORY)).Product Name:.;
WHERE WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.STATE_PROV_NAME EQ &RECIPIENT_STATE_NAME.(OR(FIND WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.STATE_PROV_NAME IN RETAIL_SAMPLES/WF_RETAIL |FORMAT=A40V,SORT=ASCENDING)).State or Province:.;
WHERE WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_MONTH_COMPONENT GE &ORDER_MONTH_FROM.(FIND WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_MONTH_COMPONENT IN RETAIL_SAMPLES/WF_RETAIL |FORMAT=YYMDm,SORT=ASCENDING).Order Month From:.QUOTEDSTRING;
WHERE WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_MONTH_COMPONENT LE &ORDER_MONTH_TO.(FIND WF_RETAIL.WF_RETAIL_TIME_SALES.TIME_DATE_MONTH_COMPONENT IN RETAIL_SAMPLES/WF_RETAIL |FORMAT=YYMDm,SORT=DESCENDING).Order Month To:.QUOTEDSTRING;
WHERE WF_RETAIL.WF_RETAIL_STORE_SALES.STORE_TYPE NE '&WHERE_EXCLUDE_STATE';
WHERE WF_RETAIL.WF_RETAIL_GEOGRAPHY_CUSTOMER.COUNTRY_NAME EQ 'Canada' OR 'United States';

WHERE &DRILL_FIELD EQ '&DRILL_VALUE';
WHERE &DRILL_FIELD2 EQ '&DRILL_VALUE2';
HEADING
"Shipment details &HEADING_EXCLUDE_STATE"
"for &DRILL_FIELD_HEADING = &DRILL_VALUE "
"and &DRILL_FIELD2_HEADING = &DRILL_VALUE2 "

ON TABLE SET PAGE-NUM NOLEAD
ON TABLE SET ASNAMES ON
ON TABLE NOTOTAL
ON TABLE PCHOLD FORMAT AHTML
ON TABLE SET HTMLEMBEDIMG ON
ON TABLE SET HTMLCSS ON
ON TABLE SET CACHELINES 100
ON TABLE SET WEBVIEWTARG OFF
ON TABLE SET WEBVIEWER ON

ON TABLE SET STYLE *
     INCLUDE = IBFS:/EDA/EDASERVE/_EDAHOME/ETC/warm.sty,
$
TYPE=DATA,SIZE=10,
$
TYPE=TITLE,SIZE=10,
$

ENDSTYLE
END
